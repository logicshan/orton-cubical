----------------------------------------------------------------------
-- This Agda code is designed to accompany the thesis "Cubical models
-- of homotopy type theory - an internal approach" by Ian Orton.
--
-- The definition of a DemiEndo and its use in showing that definitions
-- preserve transport structure is due to Evan Cavallo.
----------------------------------------------------------------------

{-# OPTIONS --rewriting #-}
module hits where 

open import prelude
open import impredicative
open import interval
open import cof
open import fibrations
open import hcomp-trans
open import quotients
open import replace

----------------------
-- ğ•ŠÂ¹ (the circle)
----------------------
open import Data.paths

data Endpoints : Int â†’ Int â†’ Set where
  I=O : Endpoints I O

preğ•ŠÂ¹ : Set
preğ•ŠÂ¹ = Int / Endpoints

-- Define ğ•ŠÂ¹ as the fibrewise fibrant replacement of
-- preğ•ŠÂ¹ (the interval with the endpoints equated) 
ğ•ŠÂ¹ : Set
ğ•ŠÂ¹ = Replace preğ•ŠÂ¹

ğ•ŠÂ¹HComp : isHComp ğ•ŠÂ¹
ğ•ŠÂ¹HComp = replaceIsHComp preğ•ŠÂ¹

-- The base point
base : ğ•ŠÂ¹
base = Î¹ ([ O ]/ Endpoints)

-- The loop from base to base
loop : base ~ base
loop = p , refl , cong Î¹ (qeq Endpoints I=O) where
  p : Int â†’ ğ•ŠÂ¹
  p i = Î¹ ([ i ]/ Endpoints)

-- The elimination principle
ğ•ŠÂ¹-elim :
  (P : ğ•ŠÂ¹ â†’ Set)
  (Ï : isFib P)
  (a : P base)
  (l : (i : Int) â†’ P (loop at i))
  (lO : subst P (atO loop) (l O) â‰¡ a)
  (lI : subst P (atI loop) (l I) â‰¡ a)
  â†’ ---------------------
  (x : ğ•ŠÂ¹) â†’ P x
ğ•ŠÂ¹-elim P Ï a l lO lI x = replaceInd preğ•ŠÂ¹ P Ï f x where
  f : (x : preğ•ŠÂ¹) â†’ P (Î¹ x)
  f = qind Endpoints (P âˆ˜ Î¹) l resp where
    resp : (i j : Int) (r : Endpoints i j) â†’
      subst (P âˆ˜ Î¹) (qeq Endpoints r) (l i) â‰¡ l j
    resp .I .O I=O =
      proof:
        subst (P âˆ˜ Î¹) (qeq Endpoints I=O) (l I)
          â‰¡[ substcong P Î¹ (qeq Endpoints I=O) (l I) ]â‰¡
        subst P (cong Î¹ (qeq Endpoints I=O)) (l I)
          â‰¡[ lI ]â‰¡
        a
          â‰¡[ symm lO ]â‰¡
        l O
      qed

-- ğ•ŠÂ¹ is always a fibration (over any context)
ğ•ŠÂ¹Fib : {Î“ : Set} â†’ isFib {Î“ = Î“} Î» _ â†’ ğ•ŠÂ¹
ğ•ŠÂ¹Fib = toFib (Î» _ â†’ ğ•ŠÂ¹HComp) (constTransp ğ•ŠÂ¹)

-----------------------------
-- Suspension (of an object)
-----------------------------
data preSusp (X : Set) : Set where
  preNorth : preSusp X
  preSouth : preSusp X
  preMerid : X â†’ Int â†’ preSusp X

data MeridEnds {X : Set} : preSusp X â†’ preSusp X â†’ Set where
  meridO : (x : X) â†’ MeridEnds (preMerid x O) preNorth
  meridI : (x : X) â†’ MeridEnds (preMerid x I) preSouth

-- Quotient by sending the endpoints of merid x to north and south
preSusp' : Set â†’ Set
preSusp' X = preSusp X / MeridEnds

-- Then fibrewise fibrantly replace to get Susp
Susp : Set â†’ Set
Susp X = Replace (preSusp' X)

-- The terms north and south, and the paths merid
north : {X : Set} â†’ Susp X
north = Î¹ ([ preNorth ]/ MeridEnds)

south : {X : Set} â†’ Susp X
south = Î¹ ([ preSouth ]/ MeridEnds)

merid : {X : Set} â†’ X â†’ north ~ south
merid {X} x = p , (cong Î¹ (qeq MeridEnds (meridO x))) , cong Î¹ (qeq MeridEnds (meridI x)) where
  p : Int â†’ Susp X
  p i = Î¹ ([ preMerid x i ]/ MeridEnds)

-- Elimination/induction principle
Susp-elim :
  (X  : Set)
  (P  : Susp X â†’ Set)
  (Ï  : isFib P)
  (an : P north)
  (as : P south)
  (al : (x : X)(i : Int) â†’ P (merid x at i))
  (lO : (x : X) â†’ subst P (atO (merid x)) (al x O) â‰¡ an)
  (lI : (x : X) â†’ subst P (atI (merid x)) (al x I) â‰¡ as)
  â†’ ---------------------
  (x : Susp X) â†’ P x
Susp-elim X P Ï an as al lO lI = replaceInd _ P Ï f where
  f : (x : preSusp X / MeridEnds) â†’ P (Î¹ x)
  f = qind MeridEnds (P âˆ˜ Î¹) f' resp where
    f' : (x : preSusp X) â†’ (P âˆ˜ Î¹) ([ x ]/ MeridEnds)
    f' preNorth = an
    f' preSouth = as
    f' (preMerid x i) = al x i
    resp : (x y : preSusp X) (r : MeridEnds x y) â†’
      subst (P âˆ˜ Î¹) (qeq MeridEnds r) (f' x) â‰¡ f' y
    resp .(preMerid x O) .preNorth (meridO x) =
      proof:
        subst (P âˆ˜ Î¹) (qeq MeridEnds (meridO x)) (al x O)
          â‰¡[ substcong P Î¹ (qeq MeridEnds (meridO x)) (al x O) ]â‰¡
        subst P (cong Î¹ (qeq MeridEnds (meridO x))) (al x O)
          â‰¡[ lO x ]â‰¡
        an
      qed
    resp .(preMerid x I) .preSouth (meridI x) =
      proof:
        subst (P âˆ˜ Î¹) (qeq MeridEnds (meridI x)) (al x I)
          â‰¡[ substcong P Î¹ (qeq MeridEnds (meridI x)) (al x I) ]â‰¡
        subst P (cong Î¹ (qeq MeridEnds (meridI x))) (al x I)
          â‰¡[ lI x ]â‰¡
        as
      qed

-- preSusp' is a DemiEndo
preSuspEndo : DemiEndo
preSuspEndo = endo preSusp' func pres-id where

  preSuspFunc : {A B : Set} â†’ (A â†’ B) â†’ preSusp A â†’ preSusp B
  preSuspFunc _ preNorth = preNorth
  preSuspFunc _ preSouth = preSouth
  preSuspFunc f (preMerid x i) = preMerid (f x) i

  func : {A B : Set} â†’ (A â†’ B) â†’ preSusp' A â†’ preSusp' B
  func {A} {B} f = qind MeridEnds _ (Î» x â†’ [ preSuspFunc f x ]/ MeridEnds) resp where
    resp : (x y : preSusp A) (r : MeridEnds x y) â†’
      subst _ (qeq MeridEnds r) ([ preSuspFunc f x ]/ MeridEnds) â‰¡ [ preSuspFunc f y ]/ MeridEnds
    resp .(preMerid x O) .preNorth (meridO x) =
      trans (qeq MeridEnds (meridO (f x)))
        (substconst (preSusp' B) (qeq MeridEnds (meridO x)) _)
    resp .(preMerid x I) .preSouth (meridI x) =
      trans (qeq MeridEnds (meridI (f x)))
        (substconst (preSusp' B) (qeq MeridEnds (meridI x)) _)

  pres-id : (A : Set) â†’ func id â‰¡ id
  pres-id A =
    funext (qind MeridEnds (Î» x â†’ func id x â‰¡ x)
      (Î»{ preNorth â†’ refl ; preSouth â†’ refl ; (preMerid x xâ‚) â†’ refl})
      (Î» _ _ _ â†’ uipImp)
    )

----------------------------------------------------------------------
-- Therefore Suspension is a DemiEndo, and hence preserves transport
-- structure
----------------------------------------------------------------------

endoComp : (g f : DemiEndo) â†’ DemiEndo
endoComp (endo g gfunc gid) (endo f ffunc fid) =
  endo (g âˆ˜ f) (Î» h â†’ gfunc (ffunc h)) (Î» A â†’ trans (gid (f A)) (cong gfunc (fid A)))

SuspEndo : DemiEndo
SuspEndo = endoComp replaceEndo preSuspEndo

SuspEndoIsSusp : obf SuspEndo â‰¡ Susp
SuspEndoIsSusp = refl

SuspIsTransp : {Î“ : Set}(X : Î“ â†’ Set) â†’ isTransp X â†’ isTransp (Susp âˆ˜ X)
SuspIsTransp X tX = TranspFibEndo SuspEndo X tX

----------------------------------------------------------------------
-- Hence Suspension preserves fibration structure
----------------------------------------------------------------------
SuspIsFib : {Î“ : Set}(X : Î“ â†’ Set) â†’ isFib X â†’ isFib (Susp âˆ˜ X)
SuspIsFib X Î± = toFib (Î» x â†’ replaceIsHComp (preSusp' (X x))) (SuspIsTransp X (snd (fromFib Î±)))


----------------------------------------------------------------------
-- Pushout
----------------------------------------------------------------------
data prePushout {X Y Z : Set}(f : X â†’ Y)(g : X â†’ Z) : Set where
  preLeft  : Y â†’ prePushout f g
  preRight : Z â†’ prePushout f g
  prePush  : X â†’ Int â†’ prePushout f g

data PushEnds {X Y Z}{f : X â†’ Y}{g : X â†’ Z} : prePushout f g â†’ prePushout f g â†’ Set where
  pushO : (x : X) â†’ PushEnds (prePush x O) (preLeft (f x))
  pushI : (x : X) â†’ PushEnds (prePush x I) (preRight (g x))

-- Quotient as before
prePushout' : {X Y Z : Set}(f : X â†’ Y)(g : X â†’ Z) â†’ Set
prePushout' f g = prePushout f g / PushEnds

-- Then fibrewise fibrantly replace to get Pushout
Pushout : {X Y Z : Set}(f : X â†’ Y)(g : X â†’ Z) â†’ Set
Pushout f g = Replace (prePushout' f g)

-- The terms left and right and the paths push
left : âˆ€{X Y Z}{f : X â†’ Y}{g : X â†’ Z} â†’ Y â†’ Pushout f g
left y = Î¹ ([ preLeft y ]/ PushEnds)

right : âˆ€{X Y Z}{f : X â†’ Y}{g : X â†’ Z} â†’ Z â†’ Pushout f g
right z = Î¹ ([ preRight z ]/ PushEnds)

push : âˆ€{X Y Z}{f : X â†’ Y}{g : X â†’ Z} â†’ (x : X) â†’ left (f x) ~ right (g x)
push {f = f} {g} x = p , (cong Î¹ (qeq PushEnds (pushO x)) , cong Î¹ (qeq PushEnds (pushI x))) where
  p : Int â†’ Pushout f g
  p i = Î¹ ([ prePush x i ]/ PushEnds)

-- Elimination/induction principle
Pushout-elim :
  {X Y Z : Set}
  (f  : X â†’ Y)
  (g  : X â†’ Z)
  (P  : Pushout f g â†’ Set)
  (Ï  : isFib P)
  (al : (y : Y) â†’ P (left y))
  (ar : (z : Z) â†’ P (right z))
  (ap : (x : X)(i : Int) â†’ P (push x at i))
  (pO : (x : X) â†’ subst P (atO (push x)) (ap x O) â‰¡ al (f x))
  (pI : (x : X) â†’ subst P (atI (push x)) (ap x I) â‰¡ ar (g x))
  â†’ ---------------------
  (x : Pushout f g) â†’ P x
Pushout-elim f g P Ï al ar ap pO pI = replaceInd _ P Ï h where
  h : (a : prePushout' f g) â†’ P (Î¹ a)
  h = qind PushEnds (P âˆ˜ Î¹) h' resp where
    h' : (x : prePushout f g) â†’ (P âˆ˜ Î¹) ([ x ]/ PushEnds)
    h' (preLeft y) = al y
    h' (preRight z) = ar z
    h' (prePush x i) = ap x i
    resp : (x y : prePushout f g) (r : PushEnds x y) â†’
      subst (P âˆ˜ Î¹) (qeq PushEnds r) (h' x) â‰¡ h' y
    resp .(prePush x O) .(preLeft _) (pushO x) =
      proof:
        subst (P âˆ˜ Î¹) (qeq PushEnds (pushO x)) (ap x O)
          â‰¡[ substcong P Î¹ (qeq PushEnds (pushO x)) (ap x O) ]â‰¡
        subst P (cong Î¹ (qeq PushEnds (pushO x))) (ap x O)
          â‰¡[ pO x ]â‰¡
        al (f x)
      qed
    resp .(prePush x I) .(preRight _) (pushI x) =
      proof:
        subst (P âˆ˜ Î¹) (qeq PushEnds (pushI x)) (ap x I)
          â‰¡[ substcong P Î¹ (qeq PushEnds (pushI x)) (ap x I) ]â‰¡
        subst P (cong Î¹ (qeq PushEnds (pushI x))) (ap x I)
          â‰¡[ pI x ]â‰¡
        ar (g x)
      qed

